plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("org.jetbrains.kotlin.plugin.compose")
}

android {
    namespace = "dev.relic.android"
    compileSdk = 36

    defaultConfig {
        applicationId = "dev.relic"
        minSdk = 26
        targetSdk = 36
        versionCode = 1
        versionName = "0.1.0-alpha"
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        compose = true
    }

    sourceSets.getByName("main") {
        // UniFFI-generated bindings straight from the Rust build
        // (regenerate with ffi/uniffi/generate-kotlin.ps1).
        java.srcDir("../../../ffi/uniffi/out/kotlin")
        // librelic_ffi.so per ABI, produced by tools/android/build-apk.ps1
        // via cargo-ndk.
        jniLibs.srcDir("src/main/jniLibs")
    }
}

dependencies {
    val composeBom = platform("androidx.compose:compose-bom:2024.12.01")
    implementation(composeBom)
    implementation("androidx.core:core-ktx:1.15.0")
    implementation("androidx.activity:activity-compose:1.9.3")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.8.7")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.ui:ui")
    implementation("io.coil-kt:coil-compose:2.7.0")
    // UniFFI runtime requirement
    implementation("net.java.dev.jna:jna:5.15.0@aar")
}
